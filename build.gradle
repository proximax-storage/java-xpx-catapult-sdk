/*
 * Copyright 2018 NEM
 * Copyright 2019 ProximaX
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    // main language plugin
    id 'java-library'
    // distribution of library and dependencies
    id 'java-library-distribution'
    // publishing to maven repository
    id 'maven-publish'
    // sign the artifacts and pom
    id 'signing'
    // code coverage reporting
    id 'jacoco'
    // integrate coverage with github
    id 'com.github.kt3k.coveralls' version '2.8.4'
    // generator for the DTOs
    id "org.openapi.generator" version "4.0.2"
}

// define contents of the build
apply from: "$rootDir/gradle/content.gradle"
// load setup for integration tests
apply from: "$rootDir/gradle/integration.gradle"
// load setup for coverage reports
apply from: "$rootDir/gradle/coverage.gradle"
// loak setup for publishing
apply from: "$rootDir/gradle/publishing.gradle"
// load Travis CI specific support
apply from: "$rootDir/gradle/travis.gradle"
// load support for openapi code generator
apply from: "$rootDir/gradle/openapi.gradle"

// Java8
sourceCompatibility = 1.8

// take dependencies from maven central
repositories {
    mavenCentral()
}

// add generated code to the man sources et
sourceSets {
    main {
    	java {
    		srcDirs += [ 'src/gen/java' ]
    	}
    }
}

// testing details
test {
    // use JUnit5
    useJUnitPlatform()
}

// versions of dependencies
ext {
    okHttp3Version = '3.14.1'
    junitVersion = '5.4.2'
}

dependencies {
    // ReactiveX async processing
    api 'io.reactivex.rxjava2:rxjava:2.2.11'
    
    // backport of java.time to support old android devices - removed from dependencies to avoid classpath polution
	compileOnly 'org.threeten:threetenbp:1.4.0'

	// crypto utilities, Hex
    implementation('com.madgag.spongycastle:prov:1.58.0.0') {
    	// exclude junit as transitive dependency...
    	exclude group: 'junit'
    }
    // validations
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    // sparse bitmap implementation
    implementation 'com.googlecode.javaewah:JavaEWAH:1.1.6'
    // serialization of tranactions for server
    implementation 'com.google.flatbuffers:flatbuffers-java:1.11.0'
	// dependencies of DTO code generated by swagger    
    implementation 'com.google.code.gson:gson:2.8.5'
	implementation 'io.gsonfire:gson-fire:1.8.3'
    implementation 'io.swagger:swagger-annotations:1.5.22'
    implementation "com.squareup.okhttp3:okhttp:${okHttp3Version}"
	implementation "com.squareup.okhttp3:logging-interceptor:${okHttp3Version}"
	
	// file utils
	testImplementation 'commons-io:commons-io:2.4'
    // backport of java.time to support old android devices
	testImplementation 'org.threeten:threetenbp:1.4.0'
	
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'org.mockito:mockito-all:1.9.5'
    testImplementation 'com.googlecode.matrix-toolkits-java:mtj:1.0.1'
	
    // Logging facade
	testImplementation 'org.slf4j:slf4j-api:1.7.26'
	// log implementation
	testRuntimeOnly 'ch.qos.logback:logback-classic:1.2.3'
	
    // Tests
    testImplementation 'org.apiguardian:apiguardian-api:1.0.0'
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}